#
# Executes commands at login pre-zshrc.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

#
# Browser
#

if [[ "$OSTYPE" == darwin* ]]; then
  export BROWSER='open'
fi

#
# Editors
#

export EDITOR='vim'
export VISUAL='vim'
export PAGER='less'

#
# Language
#

if [[ -z "$LANG" ]]; then
  export LANG='en_US.UTF-8'
fi

#
# Paths
#

# Ensure path arrays do not contain duplicates.
typeset -gU cdpath fpath mailpath path

# Set the the list of directories that cd searches.
# cdpath=(
#   $cdpath
# )

# Set the list of directories that Zsh searches for programs.
path=(
  /usr/local/{bin,sbin}
  $path
)

#
# Less
#

# Set the default Less options.
# Mouse-wheel scrolling has been disabled by -X (disable screen clearing).
# Remove -X and -F (exit if the content fits on one screen) to enable it.
export LESS='-F -g -i -M -R -S -w -X -z-4'

# Set the Less input preprocessor.
if (( $+commands[lesspipe.sh] )); then
  export LESSOPEN='| /usr/bin/env lesspipe.sh %s 2>&-'
fi

#
# Temporary Files
#

if [[ ! -d "$TMPDIR" ]]; then
  export TMPDIR="/tmp/$USER"
  mkdir -p -m 700 "$TMPDIR"
fi

TMPPREFIX="${TMPDIR%/}/zsh"
if [[ ! -d "$TMPPREFIX" ]]; then
  mkdir -p "$TMPPREFIX"
fi

#
# Local customisations
#

#
# Set some variables expected by packages. Harmless if packages aren't
# present
#

# define a download cache for pip. A bit os X centric at present
export PIP_DOWNLOAD_CACHE=$HOME/Library/Caches/pip-downloads

#
# Local path customisations
#

# Set the brew prefix if brew exists and add additional dirs to path
if (( $+commands[brew] )); then
  brew_prefix=$(brew --prefix)
    if [[ -d ${brew_prefix}/share/npm ]] ; then
    path=(
      ${brew_prefix}/share/npm/bin
      $path
    )
    export BYOBU_PREFIX=${brew_prefix}
  fi
fi

# also define GOROOT if go is installed
if (( $+commands[go] )); then
  # define the GOPATH as required by golang
  export GOPATH=${HOME}/go
  export GOROOT=$(go env GOROOT)
  path=(
    ${GOROOT}/bin
    ${GOPATH}/bin
    $path
  )
  if (( $+commands[launchctl] )); then
    launchctl setenv GOPATH ${GOPATH}
    launchctl setenv GOROOT ${GOROOT}
  fi
fi

# set up GROOVY_HOME if installed
if (( $+commands[groovy] )); then
  export GROOVY_HOME=${brew_prefix}/opt/groovy/libexec
  if (( $+commands[launchctl] )); then
    launchctl setenv GROOVY_HOME ${GROOVY_HOME}
  fi
fi

# Setup paths for haskell-platform if installed
if (( $+commands[ghci] )); then
  # define cabal location and add bin dir to path
  export CABALPATH="${HOME}/.cabal"
  path=(
    ${CABALPATH}/bin
    $path
  )
fi

# Set JAVA_HOME if Java is installed
if [[ -x /usr/libexec/java_home ]]; then
  export JAVA_HOME=$(/usr/libexec/java_home)
fi

# Setup cask http://cask.github.io if installed
if [[ -d ${HOME}/.cask ]]; then
  path=(
    ${HOME}/.cask/bin
    $path
  )
fi

# Set more directories that Zsh searches for programs.
path=(
  ${HOME}/bin
  $path
)
